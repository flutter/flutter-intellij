/*
 * Copyright 2020 The Chromium Authors. All rights reserved.
 * Use of this source code is governed by a BSD-style license that can be
 * found in the LICENSE file.
 */

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
}

apply plugin: 'java'
apply plugin: "org.jetbrains.intellij"

group = 'io.flutter'
version = flutterPluginVersion

java {
  sourceCompatibility = JavaVersion.toVersion(javaVersion)
  targetCompatibility = JavaVersion.toVersion(javaVersion)
}

intellij {
  // This adds nullability assertions, but also compiles forms.
  if (testing == false) {
    instrumentCode = true
  }
  updateSinceUntilBuild = false
  downloadSources = false
  localPath = "${project.rootDir.absolutePath}/artifacts/$ide"
  plugins = ["java", "Dart:$dartVersion", "properties", "junit",
             "gradle", "Groovy", "smali", "IntelliLang", "org.jetbrains.android"]
}

sourceSets {
  main.java.srcDirs = ['src/main/java']
  test.java.srcDirs = ['src/main/java']
}

buildSearchableOptions.enabled = false

dependencies {
  compileOnly project(':flutter-idea')
  testImplementation project(':flutter-idea')
  compileOnly fileTree(dir: "$project.rootDir/artifacts/android-studio/lib",
                       includes: ["*.jar"])
  testImplementation fileTree(dir: "$project.rootDir/artifacts/android-studio/lib",
                       includes: ["*.jar"])
  compileOnly fileTree(dir: "$project.rootDir/artifacts/android-studio/plugins",
                       includes: ["**/*.jar"],
                       excludes: ["**/kotlin-compiler.jar", "**/kotlin-plugin.jar"])
  testImplementation fileTree(dir: "$project.rootDir/artifacts/android-studio/plugins",
                       includes: ["**/*.jar"],
                       excludes: ["**/kotlin-compiler.jar", "**/kotlin-plugin.jar"])
}

//noinspection GroovyAssignabilityCheck
tasks {
  instrumentCode {
    compilerVersion = "$baseVersion"
  }
  instrumentTestCode {
    compilerVersion = "$baseVersion"
  }
}

verifyPlugin {
  ignoreFailures = true
}
