/*
 * Copyright 2020 The Chromium Authors. All rights reserved.
 * Use of this source code is governed by a BSD-style license that can be
 * found in the LICENSE file.
 */

repositories {
  mavenLocal()
  maven {
      url "https://www.jetbrains.com/intellij-repository/snapshots/"
    }

  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
  maven {
    url 'https://jetbrains.bintray.com/intellij-plugin-service'
  }
  maven {
    url 'https://jetbrains.bintray.com/intellij-third-party-dependencies'
  }
  maven {
    url 'https://www.jetbrains.com/intellij-repository/releases'
  }
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: "org.jetbrains.intellij"

group 'io.flutter'
version=flutterPluginVersion

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = javaVersion
  }
}

java {
  sourceCompatibility = JavaVersion.toVersion(javaVersion)
  targetCompatibility = JavaVersion.toVersion(javaVersion)
}

intellij {
  // This adds nullability assertions, but also compiles forms.
  instrumentCode = true
  updateSinceUntilBuild = false
  downloadSources false
  localPath "${project.rootDir.absolutePath}/artifacts/$ide"
  plugins "java", "Dart:$dartVersion", "properties", "junit", "Kotlin", "Git4Idea",
          "gradle", "Groovy", "smali", "IntelliLang", "org.jetbrains.android", "yaml"
}

dependencies {
  testImplementation "org.powermock:powermock-api-mockito2:2.0.0"
  testImplementation "org.powermock:powermock-module-junit4:2.0.0"
  if ("$ide" == 'android-studio') {
    testImplementation project(':flutter-studio')
    testRuntime fileTree(dir: "$project.rootDir/artifacts/android-studio/plugins",
                         includes: ["**/*.jar"],
                         excludes: ["**/kotlin-compiler.jar", "**/kotlin-plugin.jar"])
    testRuntime fileTree(dir: "$project.rootDir/artifacts/android-studio/lib",
                         includes: ["*.jar"])
    compileOnly fileTree(dir: "$project.rootDir/artifacts/android-studio/plugins/git4idea/lib",
                         includes: ["*.jar"])
    testImplementation fileTree(dir: "$project.rootDir/artifacts/android-studio/plugins/git4idea/lib",
                         includes: ["*.jar"])
  } else {
    compileOnly fileTree(dir: "$project.rootDir/artifacts/ideaIC/plugins/git4idea/lib",
                         includes: ["*.jar"])
    testImplementation fileTree(dir: "$project.rootDir/artifacts/ideaIC/plugins/git4idea/lib",
                         includes: ["*.jar"])
  }
  compileOnly fileTree(dir: "$project.rootDir/lib/jxbrowser",
                       includes: ["*.jar"])
  testImplementation fileTree(dir: "$project.rootDir/lib/jxbrowser",
                       includes: ["*.jar"])
  testImplementation 'junit:junit:4.13'
}

/* Need to swizzle source and resource paths to match standard or buildPlugin does nothing. */
sourceSets {
  main.java.srcDirs = [
    "src/main/java",
    "gen",
    "third_party/vmServiceDrivers",
    "src/main/resources"
  ]
  // Add main.kotlin.srcDirs if we start using Kotlin in the main plugin.
  main.resources.srcDirs = [
    "src/main/java",
    "src/main/resources",
  ]
  test.java.srcDirs = [
    "gen",
    "src/main/java",
    "testSrc/unit",
    "third_party/vmServiceDrivers"
  ]
  test.resources.srcDirs = [
    "src/main/resources",
    "testData/unit",
    "testSrc/unit"
  ]
}

buildSearchableOptions.enabled = false

check.dependsOn verifyPlugin

test {
  useJUnit()
}
